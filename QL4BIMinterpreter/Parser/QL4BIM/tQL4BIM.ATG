COMPILER QL4BIM
public delegate void PartParsedEventHandler(object sender, PartParsedEventArgs e);
public event PartParsedEventHandler PartParsed;
 
protected virtual void OnParsed(ParserParts context, string currentToken = "", string lookAhead = "") 
{	
	var e = new PartParsedEventArgs(context, currentToken, lookAhead);
    Console.WriteLine(e.ToString());
    PartParsed?.Invoke(this, e);
}


CHARACTERS

	digit = "0123456789".
	up = "ABCDEFGHIJKLMNOPQRSTUVWXYZ".
	low = "abcdefghijklmnopqrstuvwxyz".
	specials =":.-_\\".
	cr  = '\r'.
	lf  = '\n'.
	tab = '\t'.
	str = up + low + digit.
	add = '+'.
	minus = '-'.
 

TOKENS
	float = [add|minus] digit {digit} '.' {digit} ['E' [add|minus] digit {digit}].
	number = [add|minus] digit {digit}.
	lowString = low {low | up | digit}.
	upString = up {low | up  | digit}.
	string_ = '\"' { str | specials } '\"'.
	minust = minus.
	alias = ":" (up | low)(up | low | digit)[up | low | digit].

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf
IGNORE cr + lf + tab

PRODUCTIONS

	QL4BIM												
	= 		
	(																(.  OnParsed(ParserParts.GlobalBlock); .)
	statement_ {statement_} {func_})
	|
	( func_ {func_})
	.						
	/*=====================================================*/

	statement_														(.  OnParsed(ParserParts.Statement); .)	
	=																
	lowString														(.  OnParsed(ParserParts.Variable, t.val); .)						
	{																
	"[]"															(.  OnParsed(ParserParts.EmptyRelAtt); .)
	|"[" lowString													(.  OnParsed(ParserParts.RelAtt); .)			
	 "|" lowString													(.  OnParsed(ParserParts.RelAtt); .)
	{"|" lowString}													(.  OnParsed(ParserParts.RelAtt); .)
	"]"
	}
	"="													
	expression
	.						
	/*=====================================================*/

	expression														
	= 
	upString														(.  OnParsed(ParserParts.Operator, t.val); .)
	"("													
		argument													
	{ ","										
		argument								
	} ")"
	.
	/*=====================================================*/

	argument			
	=
	setRelAttLongShort [exType | exAtt [attPredicate]  | countPredicate ] 					
	|																				
	constant												
	.
	/*=====================================================*/

	constant =  
	string_ 												(.  OnParsed(ParserParts.String, t.val); .)	
	| number 												(.  OnParsed(ParserParts.Number, t.val); .)	
	| float 												(.  OnParsed(ParserParts.Float, t.val); .)	
	| bool_														
	.
	/*=====================================================*/

	bool_ 
	=														
	"true" 													(.  OnParsed(ParserParts.Bool,t.val); .)
	| "false" 												(.  OnParsed(ParserParts.Bool,t.val); .)
	| "unknown"												(.  OnParsed(ParserParts.Bool,t.val); .)
	.
	/*=====================================================*/

	attPredicate											(.  OnParsed(ParserParts.AttPredicate); .)	
	=
	 equalsPred | inPred | morePred | moreEqulPred | lessPred | lessEqulPred
	 .
	/*=====================================================*/

	equalsPred												(.  OnParsed(ParserParts.EqualsPred); .)	
	= "=" (constant | setRelAttPredEnd).

	inPred													(.  OnParsed(ParserParts.InPred); .)	
	= "~" (string_ | setRelAttPredEnd).

	morePred												(.  OnParsed(ParserParts.MorePred); .)	
	= ">"  numericOrSetRelAtt.

	moreEqulPred											(.  OnParsed(ParserParts.MoreEqualPred); .)	
	= ">="  numericOrSetRelAtt.

	lessPred												(.  OnParsed(ParserParts.LessPred); .)	
	= "<"  numericOrSetRelAtt.

	lessEqulPred											(.  OnParsed(ParserParts.LessEqualPred); .)	
	= "<="  numericOrSetRelAtt.

	countPredicate											
	= (">" | ">=" | "<" | "<=" | "=") number
	.
	/*=====================================================*/

	relAtt = 
	"["		
	lowString												(.  OnParsed(ParserParts.RelAtt, t.val); .)	
	"]"
	.								
	/*=====================================================*/

	exAtt = 
	"." (lowString | upString)								(.  OnParsed(ParserParts.ExAtt, t.val); .)	
	.
	/*=====================================================*/

	exType = "is" (lowString | upString)					(.  OnParsed(ParserParts.ExType, t.val); .)	
	.
	/*=====================================================*/


	setRelAttLongShort =
	 lowString												(.  OnParsed(ParserParts.SetRelArg, t.val); .)
	 [ 
	 relAtt													(.  OnParsed(ParserParts.RelAtt, t.val); .)
	 ]												
	 | relAtt												(.  OnParsed(ParserParts.RelAtt, t.val); .)
	 .
	/*=====================================================*/

	setRelFormalArg = 
	lowString 
	[ "[" lowString "|" lowString {"|" lowString}  "]" ].
	/*=====================================================*/

	func_ =													(.  OnParsed(ParserParts.FuncDefBlock); .)
	"func" upString [alias]							
	"(" setRelFormalArg {"," setRelFormalArg } ")" 
	"(" statement_ {statement_} ")".
	/*=====================================================*/

	numericOrSetRelAtt										(.  OnParsed(ParserParts.NumericOrSetRelAtt); .)
	= 
	number | 
	float | 
	setRelAttPredEnd.
	/*=====================================================*/
	
	setRelAttPredEnd										(.  OnParsed(ParserParts.SetRelAttPredEnd); .)
	= setRelAttLongShort exAtt.

END QL4BIM.