COMPILER QL4BIM
public delegate void PartParsedEventHandler(object sender, PartParsedEventArgs e);
public event PartParsedEventHandler PartParsed;
 
protected virtual void OnParsed(PartParsedEventArgs e) 
{	
     Console.WriteLine(e.ToString());
    PartParsed?.Invoke(this, e);
}


CHARACTERS

	digit = "0123456789".
	up = "ABCDEFGHIJKLMNOPQRSTUVWXYZ".
	low = "abcdefghijklmnopqrstuvwxyz".
	specials =":.-_\\".
	cr  = '\r'.
	lf  = '\n'.
	tab = '\t'.
	str = up + low + digit.
	add = '+'.
	minus = '-'.
 

TOKENS
	float = [add|minus] digit {digit} '.' {digit} ['E' [add|minus] digit {digit}].
	number = [add|minus] digit {digit}.
	lowString = low {low | up | digit}.
	upString = up {low | up  | digit}.
	string_ = '\"' { str | specials } '\"'.
	minust = minus.
	alias = ":" (up | low)(up | low | digit)[up | low | digit].

COMMENTS FROM "/*" TO "*/" NESTED
COMMENTS FROM "//" TO lf
IGNORE cr + lf + tab

PRODUCTIONS

	QL4BIM												
	= 		
	(																(.  OnParsed(new PartParsedEventArgs("global statements")); .)
	statement_ {statement_} {func_})
	|
	( func_ {func_}).						
	/*=====================================================*/

	statement_														
	=											
	lowString														(.  OnParsed(new PartParsedEventArgs("variable")); .)							
	{													
	"[]" |
	"[" lowString											
	 "|" lowString										
	{"|" lowString}										
	"]"
	}
	"="													
	expression.						
	/*=====================================================*/

	expression														(.  OnParsed(new PartParsedEventArgs("Operator")); .)
	= 
	upString											
	"("													
		argument							
	{ ","										
		argument								
	} ")".
	/*=====================================================*/

	argument			
	=
	setRelAttLongShort [exType | exAtt [attPredicate]  | countPredicate ] 					
	|																				
	constant												(.  OnParsed(new PartParsedEventArgs("Const Argument")); .)	
	.
	/*=====================================================*/

	constant =  string_ | number | float | bool_.
	/*=====================================================*/

	bool_ = "true" | "false" | "unknown".
	/*=====================================================*/

	attPredicate = equalsPred | inPred | morePred | moreEqulPred | lessPred | lessEqulPred.
	/*=====================================================*/

	equalsPred = "=" (constant | setRelAttPredEnd).
	inPred = "~" (string_ | setRelAttPredEnd).
	morePred = ">"  numericOrSetRelAtt.
	moreEqulPred = ">="  numericOrSetRelAtt.
	lessPred = "<"  numericOrSetRelAtt.
	lessEqulPred = "<="  numericOrSetRelAtt.

	countPredicate = (">" | ">=" | "<" | "<=" | "=") number.

	/*=====================================================*/

	relAtt = "[" lowString "]".
	/*=====================================================*/

	exAtt = "." (lowString | upString).
	/*=====================================================*/

	exType = "is" (lowString | upString).
	/*=====================================================*/


	setRelAttLongShort = lowString [ relAtt ] | relAtt.
	/*=====================================================*/

	setRelFormalArg = lowString [ "[" lowString "|" lowString {"|" lowString}  "]" ].
	/*=====================================================*/
	

	setRelAttPredEnd = setRelAttLongShort exAtt.
	/*=====================================================*/

	func_ = "func" upString [alias] 
			"(" setRelFormalArg {"," setRelFormalArg } ")" 
			"(" statement_ {statement_} ")".

	numericOrSetRelAtt = number | float | setRelAttPredEnd.
END QL4BIM.